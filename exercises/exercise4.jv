transform CelsiusToFahrenheit{
  from tempCelsius oftype decimal;
  to tempFahrenheit oftype decimal;

  tempFahrenheit: (tempCelsius*9/5)+32;
}

constraint MonthConstraint oftype RangeConstraint {
    lowerBound: 1;
    upperBound: 12;
}
 
valuetype Month oftype integer {
    constraints: [MonthConstraint];
}

constraint GermanBoolConstraint oftype AllowlistConstraint {
    allowlist: ["Ja", "Nein"];
}

valuetype GermanBoolean oftype text {
    constraints: [GermanBoolConstraint];
}

pipeline BatteryPipeline {
    block DataExtractor oftype HttpExtractor {
        url: "https://www.mowesta.com/data/measure/mowesta-dataset-20221107.zip";
    }

    block ZipArchiveInterpreter oftype ArchiveInterpreter {
        archiveType: "zip";
    }

    block BatteryFilePicker oftype FilePicker {
        path: "/data.csv";
    }

    block BatteryTextFileInterpreter oftype TextFileInterpreter {}
    block BatteryCSVInterpreter oftype CSVInterpreter { delimiter: ';';}

    block BatteryColumnSelector oftype CellRangeSelector {
        select: range A1:K*;
    }

    block BatteryColumnDeleter oftype ColumnDeleter {
        delete: [column F, column G, column H, column I];
    }

    block BatteryTableInterpreter oftype TableInterpreter {
        header: false;
        columns: [
            "Geraet" oftype integer,
            "Hersteller" oftype text,
            "Model" oftype text,
            "Monat" oftype Month,
            "Temperatur" oftype decimal,
            "Batterietemperatur" oftype decimal,
            "Geraet aktiv" oftype GermanBoolean,
        ];
    }

    block TemperatureTransformer oftype TableTransformer {
        inputColumn: "Temperatur";
        outputColumn: "Temperatur";
        use: CelsiusToFahrenheit;
    }

    block BatteryTemperatureTransformer oftype TableTransformer {
        inputColumn: "Batterietemperatur";
        outputColumn: "Batterietemperatur";
        use: CelsiusToFahrenheit;
    }

    block DatabaseLoader oftype SQLiteLoader{ table: "temperatures"; file: "./temperatures.sqlite";}

    DataExtractor
        -> ZipArchiveInterpreter
        -> BatteryFilePicker
        -> BatteryTextFileInterpreter
        -> BatteryCSVInterpreter
        -> BatteryColumnSelector
        -> BatteryColumnDeleter
        -> BatteryTableInterpreter
        -> TemperatureTransformer
        -> BatteryTemperatureTransformer
        -> DatabaseLoader;
}